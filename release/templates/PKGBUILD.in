# Maintainer: Amos Wenger <amos@itch.io>
# Contributor: FrozenCow <frozencow@gmail.com>

pkgname={{CI_APPNAME}}
pkgver={{CI_VERSION}}
pkgrel={{CI_REL}}
pkgdesc="The best way to play itch.io games."

arch=('i686' 'x86_64')
url="https://github.com/itchio/{{CI_APPNAME}}"
license=('MIT')

depends=('alsa-lib' 'electron' 'libnotify' 'nss' 'gconf' 'gtk2' 'libxtst' 'desktop-file-utils' 'gtk-update-icon-cache' 'libxss')
makedepends=('nodejs' 'nodejs-grunt-cli' 'npm')
options=('!strip')
install="{{CI_APPNAME}}.install"

# sic. - source is in itch repo, kitch is a dummy repo for canary-channel github releases
source=("https://github.com/itchio/itch/archive/v${pkgver}{{CI_SUFFIX}}.tar.gz")
sha256sums=('0eafdc5c12f2a007a146b2925dc99c210ee9aeb2c859ce81fa523244098c5779')

[ "$CARCH" = "i686" ]   && _ELECTRON_ARCH=ia32; _ITCH_ARCH=i386
[ "$CARCH" = "x86_64" ] && _ELECTRON_ARCH=x64;  _ITCH_ARCH=amd64

prepare() {
  cd "itch-${pkgver}{{CI_SUFFIX}}"

  export PYTHON=/usr/bin/python2

  # Get dependencies
  # (npm3's progress indicator is notoriously slow, disable)
  npm install --no-progress --quiet
}

build() {
  cd "${srcdir}/itch-${pkgver}{{CI_SUFFIX}}"
  export CI_BUILD_TAG="v{{CI_VERSION}}{{CI_SUFFIX}}"
  export CI_CHANNEL="{{CI_CHANNEL}}"

  release/ci-compile.js
  release/ci-generate-linux-extras.js

  cd stage
  mv package.json{,_}
  grep -v types/electron < package.json_ > package.json
  npm prune --production
  npm prune --production # actually removes more packages
  mv package.json{_,}
  cd ..
  node_modules/asar/bin/asar pack stage app.asar
  echo -e "#!/bin/sh\nelectron /usr/lib/{{CI_APPNAME}}/app.asar \$*\n" > {{CI_APPNAME}}
}

check() {
  cd "${srcdir}/itch-${pkgver}{{CI_SUFFIX}}"
  node app/tests/runner.js
}

package() {
  cd "${srcdir}/itch-${pkgver}{{CI_SUFFIX}}"

  install -d "${pkgdir}/usr/lib/{{CI_APPNAME}}"
  cp -a app.asar "${pkgdir}/usr/lib/{{CI_APPNAME}}"

  install -d "${pkgdir}/usr/share/applications"
  install -Dm644 linux-extras/io.itch.{{CI_APPNAME}}.desktop "${pkgdir}/usr/share/applications/{{CI_APPNAME}}.desktop"

  for icon in release/images/{{CI_APPNAME}}-icons/icon*.png
  do
    iconsize="${icon#release/images/{{CI_APPNAME}}-icons/icon}"
    iconsize="${iconsize%.png}"
    icondir="${pkgdir}/usr/share/icons/hicolor/${iconsize}x${iconsize}/apps/"
    install -d "${icondir}"
    install -Dm644 "$icon" "$icondir/{{CI_APPNAME}}.png"
  done

  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  install -D {{CI_APPNAME}} "${pkgdir}/usr/bin/{{CI_APPNAME}}"
}
